
#ifndef __API_PATH_IDL__
#define __API_PATH_IDL__

#include <muidl.idl>
#include <l4x2.idl>
#include <posixlike.idl>

#include <api/io.idl>


module Sneks {

/* path resolution in a filesystem in which another filesystem may have been
 * attached, and which may in turn be attached to a parent filesystem.
 */
[StubPrefix(__path), IfaceLabel(0xe808)]
interface Path
{
	const long PATH_MAX = 4000;
	typedef string<PATH_MAX> path;

	typedef unsigned long object;
	typedef L4X2::word cookie;

	/* form of @ifmt */
	const long S_IFMT = 0xf000;	/* the mask (required) */
	const long S_IFDIR = 0x4000;
	const long S_IFCHR = 0x2000;
	const long S_IFBLK = 0x6000;
	const long S_IFREG = 0x8000;
	const long S_IFIFO = 0x1000;
	const long S_IFLNK = 0xa000;
	const long S_IFSOCK = 0xc000;

	/* @dirfd == 0 refers to the filesystem's root directory. this may be
	 * distinct from the filesystem's mount point, which is a directory on the
	 * parent filesystem; the exception being when the parent does not exist.
	 *
	 * @path is always relative to @dirfd, so it should never have a leading
	 * slash.
	 *
	 * TODO: remove the explicit register specs and funny label once muidl
	 * gets a nicer generated propagation feature.
	 */
	[Label(0xb00b)]
	void resolve(
		out object inode, out L4X2::ThreadId server,
		out long ifmt, out cookie cookie,
		[MR(2)] in IO::handle dirfd, in path path, [MR(3)] in long flags)
			raises(Posix::Errno, muidl::NoReply);
};

};

#endif
