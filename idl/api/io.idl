
#ifndef __API_IO_IDL__
#define __API_IO_IDL__

#include <muidl.idl>
#include <l4x2.idl>
#include <posixlike.idl>


module Sneks {

/* input/output services, i.e. character and block device providers. */
[StubPrefix(__io), IfaceLabel(0xe803)]
interface IO
{
	typedef long handle;

	/* bits for ::set_flags(). same values as on Linux. */
	const long O_NONBLOCK = 04000;

	/* this is the weak I/O interface where data is copied through the
	 * microkernel, so the max transfer size can be fairly small.
	 */
	const long IOSEG_MAX = 65536;
	typedef sequence<octet, IOSEG_MAX> ioseg;

	/* sets @fd's flags' = (flags & @and_mask) | @or_mask. returns previous
	 * flags in @old_flags. raises EBADF when @fd doesn't exist, and EINVAL
	 * when an unknown flag would be set.
	 *
	 * this alters the set of file status flags (O_*) as opposed to file
	 * descriptor flags present in userspace (FD_*), or some other set of
	 * flags accessed by ioctl.
	 */
	void set_flags(out long old_flags,
		in handle fd, in long or_mask, in long and_mask)
		raises(Posix::Errno);

	/* when write (read) blocks on a full (empty) buffer, wakeup happens in
	 * the form of an EAGAIN error result. when O_NONBLOCK is set, instead
	 * EWOULDBLOCK is raised.
	 *
	 * unlike pwrite(2), @offset can be -1 in which case the file's own
	 * position is both used and modified.
	 */
	unsigned short write(in handle fd, in Posix::off_t offset, in ioseg buf)
		raises(Posix::Errno, muidl::NoReply);

	/* unlike pread(2), @offset can be -1 in which case the file's own
	 * position is both used and modified.
	 */
	void read(in handle fd, in Posix::off_t length, in Posix::off_t offset,
		out ioseg buf)
		raises(Posix::Errno, muidl::NoReply);

	/* raises like POSIX close(2) */
	[Label(0xabcd)]	/* provisional, for epoll's use */
	void close([MR(2)] in handle fd)
		raises(Posix::Errno);

	/* dup2(2) analogue */
	[Label(0x1234)]	/* NOTE: required by tests while muidl provides no defs */
	void dup(out handle newfd, in handle oldfd)
		raises(Posix::Errno);

	/* interface for transferring file descriptors between tasks.
	 *
	 * dup_to() behaves like the two-parameter dup() but allows @receiver_pid
	 * to call touch() on @newfd to complete the ownership transfer. until
	 * touch() is called @newfd's lifetime follows the dup_to() caller's
	 * lifecycle. once touch() is called, the dup_to() caller is no longer
	 * allowed access to @newfd.
	 */
	[Label(0x1235)]	/* NOTE: required by tests, muidl doesn't provide */
	void dup_to(out handle newfd, in handle oldfd, in Posix::id_t receiver_pid)
		raises(Posix::Errno);
	void touch(in handle newfd)
		raises(Posix::Errno);
};

};

#endif
